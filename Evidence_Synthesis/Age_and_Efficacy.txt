model {
# SUB-MODEL 1: AGE-SPECIFIC PREVALENCE,
  # model parameters abbreviated by omega.age[i]
    # Note: equivalent to standard PSA, as it is 
    # sampling direclty from the prior.
  for (i in 1:10) {
    omega.age[i] ~ dlnorm(mu.a.log[i], sigma.a.log[i])
    sigma.a.log[i] ~ dgamma(0.5, .5)
  }
  
  
  
# SUB-MODEL 2: VACCINE-EFFICACY,
  # model parameters abbreviated by .vac
  for (i in 1:Nstud.vac) {
    # Likelihood
    rA.vac[i] ~ dbin(pA.vac[i], nA.vac[i])
    rB.vac[i] ~ dbin(pB.vac[i], nB.vac[i])
    # Logistic function:
    logit(pA.vac[i]) <- mu.vac[i]
    logit(pB.vac[i]) <- mu.vac[i] + delta.vac[i]
    
    # Average effect:
    mu.vac[i] ~ dnorm(0, 1e-06)
    # Random population effect:
    delta.vac[i] ~ dnorm(psi.vac, prec.vac)
  }
  
  # Priors for sub-model 2:
  psi.vac ~ dnorm(0, 1.0e-6)
  tau.vac ~ dunif(0, 100)
  prec.vac <- 1 / pow(tau.vac, 2)
  # Convert LOR to OR:
  OR.vac <- exp(psi.vac)
  # Coverting OR to probability
  # for vaccine efficacy:
  pEfficacy.vac <- 1 / (1 + OR.vac)

}

