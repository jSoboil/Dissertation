
model {

# SUB-MODEL 1: AGE-SPECIFIC PREVALENCE - model parameters abbreviated by .age. Note: 
# this is equivalent to Monte Carlo PSA, as it is technically sampling directly from 
# a prior and is not propogated into a posterior using a likelihood model. However,
# a hyperprior is used for the population variance to account for a greater uncertainty.
  for (i in 1:10) {
  # Monte Carlo model:
    omega.age[i] ~ dlnorm(mu.a.log[i], prec.age[i])
    
    # Note use of pow() function, -2 is inverse method,
    # i.e. 1 / x^2
    log(prec.age[i]) <- pow(sigma.age[i], -2)
    sigma.age[i] ~ dt(0, eta.age, 1)T(0, )
  }
  
  # Hyper-priors on pop. variance:
  eta.age ~ dunif(0, 100)
 
# END OF SUB-MODEL 1.

# SUB-MODEL 2: VACCINE-EFFICACY - model parameters abbreviated by .vac.
  for (i in 1:Nstud.vac) {
    # Likelihood:
    rA.vac[i] ~ dbin(pA.vac[i], nA.vac[i])
    rB.vac[i] ~ dbin(pB.vac[i], nB.vac[i])
    
    # Random Effect Logistic model:
    logit(pA.vac[i]) <- mu.vac[i]
    logit(pB.vac[i]) <- mu.vac[i] + delta.vac[i]
    
    # Average effect prior for sub-model 2:
    mu.vac[i] ~ dnorm(0, 1e-6)
    # Prior for sub-model 2 (Random. pop. effect):
    delta.vac[i] ~ dnorm(psi.vac, prec.vac)
  }
  
   # Hyperpriors for Sub-model 2:
   psi.vac ~ dnorm(0, 1.0e-6)
   prec.vac <- pow(tau.vac, -2)
   tau.vac ~  dunif(0, 10)
  
  # Transformations for Sub-model 2
  
   # Convert LOR to OR
   OR.vac <- exp(psi.vac)
   # Convert OR to probability
   # for vaccine efficacy
   pEfficacy.vac <- 1 / (1 + OR.vac)

# END OF SUB-MODEL 2.

# SUB-MODEL 3: CANCER PROGRESSION model parameters abbreviated by .canc. Note: 
# this is equivalent to Monte Carlo PSA, as it is technically sampling directly from 
# a prior and is not propogated into a posterior using a likelihood model. However,
# a hyperprior is used for the population variance to account for a greater uncertainty.

Stage.I.canc ~ ddirch(alpha.Stage_I)
Stage.II.canc ~ ddirch(alpha.Stage_II)
Stage.III.canc ~ ddirch(alpha.Stage_III)
Stage.IV.canc ~ dbeta(alpha.Stage_IV, beta.Stage_IV)

 }

